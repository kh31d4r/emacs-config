#+TITLE: Emacs configuration
#+AUTHOR: Daniel Persson

#+STARTUP: overview

* Global
** Display
   Don't show the startup screen.
   #+BEGIN_SRC emacs-lisp
     (setq inhibit-startup-screen t)
   #+END_SRC

   Use =dracula-theme= which looks very nice in graphical mode, and
   looks okay in most terminals.
   #+BEGIN_SRC emacs-lisp
     (use-package dracula-theme
       :ensure t
       :config (enable-theme 'dracula))
   #+END_SRC

   Start the first emacs frame maximized.
   #+BEGIN_SRC emacs-lisp
     (add-to-list 'initial-frame-alist '(fullscreen . maximized))
   #+END_SRC

   Hide menus and scrollbars.
   #+BEGIN_SRC emacs-lisp
     (menu-bar-mode 0)
     (scroll-bar-mode 0)
     (tool-bar-mode 0)
   #+END_SRC

   Flash a beacon when the cursor move around.
   #+BEGIN_SRC emacs-lisp
     (use-package beacon
       :ensure t
       :config (beacon-mode t))
   #+END_SRC

   Install =all-the-icons= for use by other packages. Use it to
   download icons, then create a =.fonts-updated= in the users
   =.emacs.d= directory to prevent it from running on every startup.
   #+BEGIN_SRC emacs-lisp
     (defun font-indicator ()
       (expand-file-name ".fonts-updated" user-emacs-directory))

     (defun fonts-need-refreshing ()
       (not (file-exists-p
           (font-indicator))))

     (defun fonts-are-updated ()
       (write-region "" nil (font-indicator)))

     (use-package all-the-icons
       :ensure t
       :config
       (when (fonts-need-refreshing)
         (all-the-icons-install-fonts t)
         (fonts-are-updated)))
   #+END_SRC
*** Interface
    Use =spaceline= with the spacemacs theme and =all-the-icons= to
    make the modeline look nicer.
    #+BEGIN_SRC emacs-lisp
      (use-package spaceline
        :ensure t
        :config
        (setq spaceline-buffer-encoding-abbrev-p nil)
        (setq spaceline-line-column-p t)
        (setq spaceline-line-p t)
        (setq spaceline-all-the-icons-separator-type 'arrow))

      (use-package spaceline-all-the-icons
        :ensure t
        :config (spaceline-all-the-icons-theme))
    #+END_SRC

    Show line numbers in programming modes, and toggle showing line
    numbers with =C-c l=.
    #+BEGIN_SRC emacs-lisp
      (add-hook 'prog-mode-hook 'display-line-numbers-mode)
      (global-set-key (kbd "C-c l") 'display-line-numbers-mode)
    #+END_SRC

    Show possible next key combinations.
    #+BEGIN_SRC emacs-lisp
      (use-package which-key
        :ensure t
        :config (which-key-mode))
    #+END_SRC
*** Text
    Increase the default font size in graphical mode.
    #+BEGIN_SRC emacs-lisp
      (set-face-attribute 'default nil :height 150)
    #+END_SRC

    Replace special characters and words with unicode symbols.
    #+BEGIN_SRC emacs-lisp
      (use-package pretty-mode
        :ensure t
        :config (global-pretty-mode t))
    #+END_SRC

    Ensure syntax highlighting is always enabled and turned up to max.
    #+BEGIN_SRC emacs-lisp
      (global-font-lock-mode t)
      (setq font-lock-maximum-decoration t)
    #+END_SRC

    Highlight matching parenthesis.
    #+BEGIN_SRC emacs-lisp
      (show-paren-mode 1)
    #+END_SRC

    Color things that look like color codes.
    #+BEGIN_SRC emacs-lisp
      (use-package rainbow-mode
        :ensure t
        :config (add-hook 'prog-mode-hook 'rainbow-mode))
    #+END_SRC
** System
   Use =helm= for emacs commands that involves filtering and selecting
   from a list. Also enable tab completion when filtering, to make
   traversing through directories easier.
   #+BEGIN_SRC emacs-lisp
     (use-package helm
       :ensure t
       :bind (("C-x C-f" . helm-find-files)
              ("C-x b" . helm-buffers-list)
              ("M-x" . helm-M-x)
              ("M-y" . helm-show-kill-ring)
              ("C-x r b" . helm-bookmarks))
       :config (define-key helm-map (kbd "C-i") 'helm-execute-persistent-action))
   #+END_SRC

   Use =projectile= to manage projects, with =projectile-helm= as
   interface.
   #+BEGIN_SRC emacs-lisp
     (use-package projectile
       :ensure t
       :bind
       ("C-c RET" . projectile-compile-project)
       ("C-c SPC" . projectile-test-project))

     (use-package helm-projectile
       :ensure t
       :config
       (global-set-key (kbd "C-x f") (lambda ()
                                         (interactive)
                                         (if (projectile-project-p)
                                             (helm-projectile-find-file)
                                           (helm-for-files))))
       (global-set-key (kbd "C-x C-b") (lambda ()
                                         (interactive)
                                         (if (projectile-project-p)
                                             (helm-projectile-switch-to-buffer)
                                           (helm-buffers-list)))))
   #+END_SRC

   Change "yes or no" to "y or n".
   #+BEGIN_SRC emacs-lisp
     (fset 'yes-or-no-p 'y-or-n-p)
   #+END_SRC

   Don't ask for command when running compile.
   #+BEGIN_SRC emacs-lisp
     (setq compilation-read-command nil)
   #+END_SRC

   Follow the compilation buffer until the first error.
   #+BEGIN_SRC emacs-lisp
     (setq compilation-scroll-output 'first-error)
   #+END_SRC

   Colorize compilation buffers.
   #+BEGIN_SRC emacs-lisp
     (ansi-color-for-comint-mode-on)
     (add-hook 'compilation-filter-hook
               (lambda ()
                 (when (eq major-mode 'compilation-mode)
                   (save-excursion
                     (ansi-color-apply-on-region compilation-filter-start (point))))))
   #+END_SRC

   Don't ask before opening large files.
   #+BEGIN_SRC emacs-lisp
     (setq large-file-warning-threshold 200000000)
   #+END_SRC

   Add newlines at the end of files that don't have them.
   #+BEGIN_SRC emacs-lisp
     (setq require-final-newline t)
   #+END_SRC

   Don't save backups in current working directory.
   #+BEGIN_SRC emacs-lisp
     (setq backup-directory-alist '(("." . "~/.emacs.d/backups")))
   #+END_SRC

   Use version numbers for the backups.
   #+BEGIN_SRC emacs-lisp
     (setq version-control t)
   #+END_SRC

   Don't ask when removing old backups.
   #+BEGIN_SRC emacs-lisp
     (setq delete-old-versions t)
   #+END_SRC

   Don't save autosaves in current working directory.
   #+BEGIN_SRC emacs-lisp
     (setq auto-save-list-file-prefix "~/.emacs.d/autosave/")
     (setq auto-save-file-name-transforms '((".*" "~/.emacs.d/autosave/" t)))
   #+END_SRC

   Write customizations to =custom.el= instead of =init.el= to avoid
   accidentally committing them. Also create the file if it doesn't
   exist, to avoid having to commit an empty file (and this making it
   useless to add it to =.gitignore=).
   #+BEGIN_SRC emacs-lisp
     (setq custom-file "~/.emacs.d/custom.el")
     (write-region "" nil custom-file)
     (load custom-file)
   #+END_SRC

   Use 24hr clock, and display time and date in a sane way.
   #+BEGIN_SRC emacs-lisp
     (setq display-time-24hr-format t)
     (setq display-time-format "%H:%M - %d %B %Y")
     (display-time-mode t)
   #+END_SRC

   Weeks start on monday.
   #+BEGIN_SRC emacs-lisp
     (setq calendar-week-start-day 1)
   #+END_SRC

   Use =UTF-8= everywhere.
   #+BEGIN_SRC emacs-lisp
     (set-terminal-coding-system 'utf-8)
     (set-keyboard-coding-system 'utf-8)
     (prefer-coding-system 'utf-8)
   #+END_SRC

   Rebind quit key in graphical mode.
   #+BEGIN_SRC emacs-lisp
     (when window-system
       (global-unset-key (kbd "C-x C-c"))
       (global-set-key (kbd "s-q") 'save-buffers-kill-terminal))
   #+END_SRC
** Editing
   Indent with 4 spaces instead of tabs.
   #+BEGIN_SRC emacs-lisp
     (setq indent-tabs-mode nil)
     (setq-default indent-tabs-mode nil)
     (setq default-tab-width 4)
   #+END_SRC

   Don't move point when pasting with middle mouse button.
   #+BEGIN_SRC emacs-lisp
     (setq mouse-yank-at-point t)
   #+END_SRC

   Try to use clipboard data from other programs when possible.
   #+BEGIN_SRC emacs-lisp
     (setq save-interprogram-paste-before-kill t)
   #+END_SRC

   Delete active region when typing.
   #+BEGIN_SRC emacs-lisp
     (delete-selection-mode 1)
   #+END_SRC

   Enable auto completion with =company=.
   #+BEGIN_SRC emacs-lisp
     (use-package company
       :ensure t
       :config
       (global-company-mode)
       (setq company-idle-delay 0)
       (setq company-minimum-prefix-length 3)
       (setq company-dabbrev-downcase nil)
       (define-key company-active-map (kbd "C-n") 'company-select-next)
       (define-key company-active-map (kbd "C-p") 'company-select-previous)
       (define-key company-active-map (kbd "TAB") 'company-abort)
       :bind ("M-a" . company-complete))

     (use-package company-box
       :ensure t
       :hook (company-mode . company-box-mode))
   #+END_SRC

   Enable snippets for all modes.
   #+BEGIN_SRC emacs-lisp
     (use-package yasnippet
       :ensure t
       :config (yas-global-mode 1))
   #+END_SRC

   Enable =expand-region=.
   #+BEGIN_SRC emacs-lisp
     (use-package expand-region
       :ensure t
       :bind ("M-e" . er/expand-region))
   #+END_SRC

   Enable =multiple-cursors=.
   #+BEGIN_SRC emacs-lisp
     (use-package multiple-cursors
       :ensure t
       :bind ("M-n" . mc/mark-next-like-this-word))
   #+END_SRC

   Don't disable upper and lowercase region, and use the =-dwim=
   versions of those commands.
   #+BEGIN_SRC emacs-lisp
     (put 'downcase-region 'disabled nil)
     (put 'upcase-region 'disabled nil)
     (global-set-key (kbd "M-u") 'upcase-dwim)
     (global-set-key (kbd "M-l") 'downcase-dwim)
   #+END_SRC

   Bind =C-c i= to spellcheck.
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c i") 'ispell-word)
   #+END_SRC

   Bind =C-c TAB= to =cleanup-tabs-whitespace=, that removes trailing
   whitespace and change tabs to spaces.
   #+BEGIN_SRC emacs-lisp
     (defun cleanup-tabs-whitespace ()
       (interactive)
       (whitespace-cleanup)
       (untabify (point-min) (point-max)))

     (global-set-key (kbd "C-c TAB") 'cleanup-tabs-whitespace)
   #+END_SRC

   Reload the current buffer from disk with =C-v=. Only prompt for
   confimation if the buffer was modified.
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-v") (lambda ()
                                   (interactive)
                                   (revert-buffer t (not (buffer-modified-p)) t)))
   #+END_SRC

   Bind =C-x e= to =eval-and-replace=, which evaluates the current
   S-expression and replaces it with the output.
   #+BEGIN_SRC emacs-lisp
     (defun eval-and-replace ()
       (interactive)
       (backward-kill-sexp)
       (condition-case nil
           (prin1 (eval (read (current-kill 0)))
                  (current-buffer))
         (error (message "Invalid expression")
                (insert (current-kill 0)))))
      (global-set-key (kbd "C-x e") 'eval-and-replace)
   #+END_SRC
*** Number manipulation
    Bind =M-+= and =M--= to incrementing or decrementing the number at point.
    #+BEGIN_SRC emacs-lisp
      (defun manipulate-number-at-point (manipulation-func)
        (interactive)
        (skip-chars-backward "0-9")
        (or (looking-at "[0-9]+")
            (error "No number at point"))
        (replace-match (number-to-string
                        (funcall manipulation-func (string-to-number (match-string 0))))))

      (global-set-key (kbd "M-+") (lambda ()
                                    (interactive)
                                    (manipulate-number-at-point #'1+)))
      (global-set-key (kbd "M--") (lambda ()
                                    (interactive)
                                    (manipulate-number-at-point #'1-)))
    #+END_SRC
** Navigation
   Disable moving point by clicking with the mouse to prevent
   accidentally moving point when trying to select a frame.
   #+BEGIN_SRC emacs-lisp
     (use-package disable-mouse
       :ensure t
       :config (global-disable-mouse-mode))
   #+END_SRC

   Treat CamelCase as different words by enabling =subword-mode=
   everywhere, and silence the comma on the modeline.
   #+begin_SRC emacs-lisp
     (global-subword-mode 1)
     (let ((entry (assq 'subword-mode minor-mode-alist)))
       (when entry (setcdr entry '(nil))))
   #+END_SRC

   Bind =C-c <left>= and =C-C <right>= to undoing and redoing changes
   to the window layout.
   #+BEGIN_SRC emacs-lisp
     (use-package winner
       :ensure t
       :config (winner-mode 1))
   #+END_SRC

   Enable fast jumping between windows when using more than 2 windows.
   #+BEGIN_SRC emacs-lisp
     (use-package ace-window
       :ensure t
       :bind ("C-x o" . ace-window))
   #+END_SRC

   Enable fast and exact jumping across the screen by binding =M-s= to =ace-jump-mode=.
   #+BEGIN_SRC emacs-lisp
     (use-package ace-jump-mode
       :ensure t
       :bind ("M-s" . ace-jump-mode))
   #+END_SRC

   Use =rotate= to change between window layouts using =C-c r= and a
   mnemonic shortcut. Note that the names are a bit backwards; they
   describe how the windows are laid out, not what way the split goes.

   | Layout          | Shortcut  | Description                                                                |
   |-----------------+-----------+----------------------------------------------------------------------------|
   | Even Horizontal | =C-c reh= | Spread evenly from left to right.                                          |
   | Even Vertical   | =C-c rev= | Spread evenly from top to bottom.                                          |
   | Main Horizontal | =C-c rmh= | Show one big window and spread the rest horizontally below.                |
   | Main Vertical   | =C-c rmv= | Show one big window and spread the rest vertically along the right.        |
   | Tiled           | =C-c rt=  | Spread out as evenly as possible over the window in both rows and columns. |

   Also bind =C-c r r= to actually rotate the windows.

   #+BEGIN_SRC emacs-lisp
     (defun set-rotate-key (mnemonic layout)
       (global-set-key (kbd (concat "C-c r" mnemonic)) layout))

     (use-package rotate
       :ensure t
       :config
       (set-rotate-key "eh" 'rotate:even-horizontal)
       (set-rotate-key "ev" 'rotate:even-vertical)
       (set-rotate-key "mh" 'rotate:main-horizontal)
       (set-rotate-key "mv" 'rotate:main-vertical)
       (set-rotate-key "t"  'rotate:tiled)
       (set-rotate-key "r"  'rotate-window))
   #+END_SRC

   Enable fast searching with =ag=, with its =helm= frontend and
   =projectile= integration. Requires the =silversearcher-ag= OS
   package to be installed to work.
   #+BEGIN_SRC emacs-lisp
     (use-package ag
       :ensure t)

     (defun helm-projectile-ag-dwim ()
       "When in a project, use the projectile version of helm-ag"
       (interactive)
       (if (projectile-project-p)
           (helm-projectile-ag)
         (helm-ag)))

     (use-package helm-ag
       :ensure t
       :bind ("C-c s" . helm-projectile-ag-dwim))
   #+END_SRC
** Version Control
   Use =magit= as git interface:
   - Make diff show inline changes
   - Customize colors to make diffs easier to read
   - When prefix is provided, open magit status in fullscreen
   #+BEGIN_SRC emacs-lisp
     (defun magit-status-fullscreen (prefix)
       (interactive "P")
       (magit-status)
       (when prefix
         (delete-other-windows)))

     (use-package magit
       :ensure t
       :bind (("C-c g" . magit-status-fullscreen))
       :config
       (setq magit-diff-refine-hunk 'all))
   #+END_SRC

   Use =git-commit-mode= for writing commit messages in git.
   #+BEGIN_SRC emacs-lisp
     (global-git-commit-mode 1)
   #+END_SRC

   Enable =flyspell-mode= when writing git commit messages.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'git-commit-mode-hook
               (lambda ()
                 (flyspell-mode)))
   #+END_SRC

   Use =git-messenger= for fast inline blame.
   #+BEGIN_SRC emacs-lisp
     (use-package git-messenger
       :ensure t
       :bind ("C-c b" . git-messenger:popup-message))
   #+END_SRC

   Highlight changed lines in the fringe with =diff-hl-mode= and
   refresh the highlight on magit actions.
   #+BEGIN_SRC emacs-lisp
     (use-package diff-hl
       :ensure t
       :config
       (global-diff-hl-mode)
       (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh))
   #+END_SRC
* Programming modes
** C/C++
   Don't use [[https://en.wikipedia.org/wiki/Indentation_style#GNU_style][GNU style]] indendataion, use [[https://en.wikipedia.org/wiki/Indentation_style#Allman_style][Allman style]] instead.
   #+BEGIN_SRC emacs-lisp
     (setq c-default-style "bsd")
   #+END_SRC

   Set indentation level to 4.
   #+BEGIN_SRC emacs-lisp
     (setq c-basic-offset 4)
   #+END_SRC

   Use =irony-mode= and =company-irony= to provide smart code
   completion. As =irony-mode= will install the =irony-server= on
   first start after installation, the following OS packages must be
   installed:
   | Package   | Version  |
   |-----------+----------|
   | =CMake=   | >= 2.8.3 |
   | =liblang= |          |

   #+BEGIN_SRC emacs-lisp
     (use-package company-irony
       :ensure t
       :config (add-to-list 'company-backends 'company-irony))

     (use-package irony
       :ensure t
       :config
       (add-hook 'c-mode-hook 'irony-mode)
       (add-hook 'c++-mode-hook 'irony-mode)
       (add-hook 'irony-mode-hook 'irony-cdb-autosetup-compile-options))

     (use-package company-c-headers
       :ensure t
       :config (add-to-list 'company-backends 'company-c-headers))
   #+END_SRC

  Use =flycheck= for syntax and style checking.
  #+BEGIN_SRC emacs-lisp
    (add-hook 'c-mode-hook 'flycheck-mode)
    (add-hook 'c++-mode-hook 'flycheck-mode)
  #+END_SRC
*** CMake
    Use =cmake-mode= for providing indentation and syntax highlighting
    when writing cmake files.
    #+BEGIN_SRC emacs-lisp
      (use-package cmake-mode
        :ensure t)
    #+END_SRC

    Use =eldoc-cmake= for quick and discreet access to =cmake=
    documentation.
    #+BEGIN_SRC emacs-lisp
      (use-package eldoc-cmake
        :ensure t
        :config (add-hook 'cmake-mode-hook (lambda ()
                                             (eldoc-cmake-enable))))
    #+END_SRC
*** Arduino
    Use =c++-mode= for arduino sketch files.
    #+BEGIN_SRC emacs-lisp
      (add-to-list 'auto-mode-alist '("\\.ino\\'" . c++-mode))
    #+END_SRC

    Define projectile project type for platformio projects, including
    compile and upload commands and project marker files. Requires
    =[[https://docs.platformio.org/en/latest/core.html][PlatformIO]]= to be installed and configured.
    #+BEGIN_SRC emacs-lisp
      (projectile-register-project-type 'platformio '("platformio.ini")
                                             :compile "platformio run -t upload"
                                             :test "platformio run")
      (add-to-list 'projectile-project-root-files "platformio.ini")
    #+END_SRC
** Cucumber
   Use =feature-mode= to edit =cucumber= files.
   #+BEGIN_SRC emacs-lisp
     (use-package feature-mode
       :ensure t)
   #+END_SRC
** Java
   Define maven commands for projectile.
   #+BEGIN_SRC emacs-lisp
     (projectile-register-project-type 'maven '("pom.xml")
                                       :compile "mvn clean install"
                                       :test "mvn clean test"
                                       :test-suffix "Test.java")

   #+END_SRC
** Lisp
   Eval the current buffer with =C-c C-v=.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'emacs-lisp-mode-hook
               (lambda ()
                 (local-set-key (kbd "C-c C-v") 'eval-buffer)))
   #+END_SRC
   Use paredit for editing elisp.
   #+BEGIN_SRC emacs-lisp
     (use-package paredit
       :ensure t
       :config (add-hook 'emacs-lisp-mode-hook #'enable-paredit-mode))
   #+END_SRC

   Turn on =eldoc= to get fast access to function signatures.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'emacs-lisp-mode-hook 'turn-on-eldoc-mode)
     (add-hook 'lisp-interaction-mode-hook 'turn-on-eldoc-mode)
   #+END_SRC

   Show parentheses in different colors to easier pair them together.
   #+BEGIN_SRC emacs-lisp
     (use-package rainbow-delimiters
       :ensure t
       :config
       (add-hook 'emacs-lisp-mode-hook (lambda ()
                                         (rainbow-delimiters-mode))))
   #+END_SRC

   Use =cask-mode= to edit =[[https://github.com/cask/cask][Cask]]= files.
   #+BEGIN_SRC emacs-lisp
     (use-package cask-mode
       :ensure t)
   #+END_SRC

   Enable =ecukes= for writing =cucumber=-like test for emacs lisp,
   with =espuds= for step definitions aimed at emacs plugin
   development.
   #+BEGIN_SRC emacs-lisp
     (use-package ecukes
       :ensure t)
     (use-package espuds
       :ensure t)
   #+END_SRC
** Python
   Use =jedi= as autocompletion engine for company, and bind hotkeys
   for goto definition and show documentation.

   Needs virtenv installed on the system to work.
   #+BEGIN_SRC emacs-lisp
     (use-package jedi
       :ensure t
       :config (add-hook 'python-mode-hook 'jedi:setup)
       :bind
       ("C-c j d" . jedi:goto-definition)
       ("C-c j p" . jedi:goto-definition-pop-marker)
       ("C-c j h" . jedi:show-doc))

     (use-package company-jedi
       :ensure t
       :config
       (add-hook 'python-mode-hook (lambda ()
                                     (add-to-list 'company-backends 'company-jedi))))
   #+END_SRC

   Use =flycheck= for syntax and style checking. Needs pylint
   installed on the system to work.
   #+BEGIN_SRC emacs-lisp
     (use-package flycheck
       :ensure t
       :config
       (add-hook 'python-mode-hook (lambda ()
                                     (flycheck-mode 1)
                                     (semantic-mode 1)
                                     (setq flycheck-checker 'python-pylint
                                           flycheck-checker-error-threshold 900))))
   #+END_SRC
** Shellscript
   Use =company-shell= for autocompletion when writing shellscripts.
   #+BEGIN_SRC emacs-lisp
     (use-package company-shell
       :ensure t
       :config (add-hook 'sh-mode-hook (lambda ()
                                         (add-to-list 'company-backends 'company-shell)
                                         (add-to-list 'company-backends 'company-shell-env))))
   #+END_SRC

   Turn on =flycheck= for syntax and style checking. Requires
   =shellchecker= to be installed on the system to work.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'sh-mode-hook 'flycheck-mode)
   #+END_SRC
** Web
   Use web mode for =html= and =css=, and make closing tags feel more
   like =nxml-mode=.
   #+BEGIN_SRC emacs-lisp
     (use-package web-mode
       :ensure t
       :mode
       (("\\.html\\'" . web-mode)
        ("\\.css\\'" . web-mode))
       :config
       (add-hook 'web-mode-hook
                 (lambda ()
                   (local-set-key
                    (kbd "C-c C-f")
                    (lambda ()
                      (interactive)
                      (web-mode-element-close)
                      (indent-for-tab-command)))))
       (setq web-mode-enable-auto-quoting t)
       (setq web-mode-enable-auto-pairing t)
       (setq web-mode-enable-auto-closing t))
    #+END_SRC

   Enable emmet mode when in =web-mode= to quickly create elements.
   #+BEGIN_SRC emacs-lisp
     (use-package emmet-mode
       :ensure t
       :config (add-hook 'web-mode-hook 'emmet-mode))
   #+END_SRC

   Start =httpd= when in =web-mode= on port 8085, and enable
   =impatient-mode=. Point browser to =http://localhost:8085/imp= to
   see the preview.
   #+BEGIN_SRC emacs-lisp
     (use-package impatient-mode
       :ensure t
       :config
       (add-hook 'impatient-mode-hook (lambda ()
                                        (setq httpd-port 8085)
                                        (httpd-start)))
       (add-hook 'web-mode-hook 'impatient-mode))
   #+END_SRC

   Enable utility functions for quickly looking things up in the HTTP
   protocol.
   #+BEGIN_SRC emacs-lisp
     (use-package know-your-http-well
       :ensure t)
   #+END_SRC
* Markup modes
** Jinja2
   Use =jinja2-mode= to edit jinja templates.
   #+BEGIN_SRC emacs-lisp
     (use-package jinja2-mode
       :ensure t
       :mode ("\\.j2\\'"))
   #+END_SRC
** JSON
   Indent JSON files with two spaces.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'json-mode-hook (lambda ()
                                 (setq js-indent-level 2)))
   #+END_SRC
   Use =flycheck= to validate JSON files. Requires =demjson= to be
   installed with pip to work.
   #+BEGIN_SRC emacs-lisp
     (use-package flycheck-demjsonlint
       :ensure t
       :config (add-hook 'json-mode-hook (lambda ()
                                           (flycheck-mode 1)
                                           (setq flycheck-checker 'json-demjsonlint))))
   #+END_SRC
** Markdown
   Use =markdown-mode= for editing markdown files.
   #+BEGIN_SRC emacs-lisp
     (use-package markdown-mode
       :ensure t
       :commands (markdown-mode gfm-mode)
       :mode (("README\\.md\\'" . gfm-mode)
              ("\\.md\\'" . markdown-mode)
              ("\\.markdown\\'" . markdown-mode))
       :config (setq markdown-command "markdown"))
   #+END_SRC
** PlantUML
   Use =plantuml-mode= for editing =[[http://plantuml.com/][plantuml]]= files, and bind =C-c
   C-p= to show a preview in other window.
   #+BEGIN_SRC emacs-lisp
     (defun plantuml-preview-other-window ()
       (interactive)
       (save-window-excursion
         (let ((current-prefix-arg '(4)))
           (call-interactively 'plantuml-preview-buffer))))

     (use-package plantuml-mode
       :ensure t
       :mode "\\.plantuml\\'"
       :config
       (setq plantuml-jar-path
             (expand-file-name "plantuml.jar" (expand-file-name "bin" user-emacs-directory)))
       (add-hook 'plantuml-mode-hook (lambda ()
                                       (local-set-key (kbd "C-c C-p") 'plantuml-preview-other-window))))
   #+END_SRC

   Use =flycheck-plantuml= to syntax check =plantuml= files.
   #+BEGIN_SRC emacs-lisp
     (use-package flycheck-plantuml
       :ensure t
       :config (add-hook 'plantuml-mode
                         (lambda ()
                           (flycheck-plantuml-setup)
                           (flycheck-mode))))
   #+END_SRC
** XML
   Set indentation size to 4.
   #+BEGIN_SRC emacs-lisp
     (setq nxml-child-indent 4)
   #+END_SRC

   Turn off the built in xml validation and use =flycheck=
   instead. Requires =xmllint= or similar to be installed on the
   system.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'nxml-mode-hook (lambda ()
                                 (rng-validate-mode -1)
                                 (flycheck-mode 1)))
   #+END_SRC
** Yaml
   Use =yaml-mode= for editing yaml files.
   #+BEGIN_SRC emacs-lisp
     (use-package yaml-mode
       :ensure t)
   #+END_SRC

   Use =flycheck= for syntax and style checking.
   #+BEGIN_SRC emacs-lisp
     (use-package flycheck-yamllint
       :ensure t
       :config (add-hook 'yaml-mode-hook (lambda ()
                                           (flycheck-yamllint-setup)
                                           (flycheck-mode))))

   #+END_SRC
*** Ansible
    When editing files that could be ansible files, enable some extra
    utilities. As there is no suitable ansible minor mode to connect
    things to right now, and creating a mode here is a bit overkill,
    just use a condition in the =yaml-mode= hook.
    #+BEGIN_SRC emacs-lisp
      (defun current-buffer-looks-like-ansible-p ()
        (if (or
             (string-prefix-p "main.yaml" (buffer-name))
             (string-prefix-p "main.yml" (buffer-name)))
            t
          nil))
    #+END_SRC

    For easy access to documentation, use =ansible-doc=.
    #+BEGIN_SRC emacs-lisp
      (use-package ansible-doc
        :ensure t
        :config (add-hook 'yaml-mode-hook (lambda ()
                                            (when (current-buffer-looks-like-ansible-p)
                                              (ansible-doc-mode 1)))))
    #+END_SRC

    Enable =company= backend specific to =ansible=.
    #+BEGIN_SRC emacs-lisp
      (use-package company-ansible
        :ensure t
        :config (add-hook 'yaml-mode-hook (lambda ()
                                            (when (current-buffer-looks-like-ansible-p)
                                              (add-to-list 'company-backends 'company-ansible)))))
    #+END_SRC
* Tools
** Docker
   Enable syntax highlighting when writing Dockerfiles.
   #+BEGIN_SRC emacs-lisp
     (use-package dockerfile-mode
       :ensure t)
   #+END_SRC

   When in programming modes, activate docker minor mode for
   controlling docker containers.
   #+BEGIN_SRC emacs-lisp
     (use-package docker
       :ensure t
       :bind ("C-c d" . docker))
   #+END_SRC
** Restclient
   Use =restclient-mode= when in =.rest= files to send rest requests.
   #+BEGIN_SRC emacs-lisp
     (use-package restclient
       :ensure t
       :mode ("\\.rest\\'" . restclient-mode))
   #+END_SRC
   Add =company-mode= completions for =restclient-mode=.
   #+BEGIN_SRC emacs-lisp
     (use-package company-restclient
       :ensure t
       :config (add-to-list 'restclient-mode-hook
                            (lambda ()
                              (add-to-list 'company-backends 'company-restclient))))
   #+END_SRC
** Shell
   Use =bash= as the default shell and bind launching a shell to =C-x
   t=. If the shell already exists, its buffer will be switched to
   instead of launching a new shell. To launch a shell named something
   other than "default-shell", use =C-c C-t= instead.
   #+BEGIN_SRC emacs-lisp
     (defconst default-shell "/bin/bash")

     (defun launch-shell (&optional shell-buffer-name)
       "Run ansi-term with DEFAULT-SHELL and SHELL-BUFFER-NAME as
     arguments. If the resulting buffer already exists, switch to it
     instead of creating a new buffer."
       (interactive)
       (let* ((final-shell-name
               (concat (or shell-buffer-name (read-string "Shell name: ")) "-shell"))
              (final-shell-buffer-name (concat "*" final-shell-name "*")))
         (if (get-buffer final-shell-buffer-name)
             (switch-to-buffer final-shell-buffer-name)
           (ansi-term
            default-shell
            final-shell-name))))

     (defun launch-default-shell ()
       "Run DEFAULT-SHELL in ansi-term in a buffer named
       \"default-shell\". If the buffer already exists, switch to it
       instead of creating it."
       (interactive)
       (launch-shell "default"))

     (global-set-key (kbd "C-x t") 'launch-default-shell)
     (global-set-key (kbd "C-x C-t") 'launch-shell)
   #+END_SRC

   Disable =beacon-mode= in shell buffers.
   #+BEGIN_SRC emacs-lisp
     (add-hook 'term-mode-hook (lambda ()
                                 (setq-local beacon-mode nil)))
   #+END_SRC
* Mail
  Using =mu4e= and =mu= for email requires them to be installed from
  [[https://github.com/djcb/mu][source]]. If they are not installed, the emacs packages will not be
  loaded.

  The =smtp_settings.el= file need to set the following variables:
  - =smtpmail-default-smtp-server=
  - =smtpmail-local-domain=
  - =smtpmail-smtp-user=
  - =smtpmail-smtp-server=
  - =smtpmail-smtp-service=

  Mail commands are prefixed with =C-c m=, with the following
  mnemonic shortcuts:
  - =m= for the =mu4e= startup screen
  - =n= to compose new mail (including any active region in the body)
  - =o= to compose new =org-mode= mail (including any active region in
    the body)

  #+BEGIN_SRC emacs-lisp
    (setq user-mail-address "daniel@silvertejp.org"
          user-full-name "Daniel Persson")

    (defun mu4e-fetch-mail-and-mu4e ()
      "Fetch mail and goto the main mu4e screen"
      (interactive)
      (mu4e-update-mail-and-index t)
      (mu4e))

    (defun mu4e-compose-mail-from-region ()
      "Create a new mu4e mail, containing the region, if active"
      (interactive)
      (let ((text (active-region-or-empty-string)))
        (mu4e-compose-new)
        (save-excursion
          (mu4e-compose-goto-top)
          (insert text))))

    (defun mu4e-compose-org-mail-from-section ()
      "Create an org-mu4e-org-mode mail, containing the current org section"
      (interactive)
      (org-mark-subtree)
      (mu4e-compose-org-mail-from-region))

    (defun mu4e-compose-org-mail-from-region ()
      "Create an org-mu4e-org-mode mail, containing the region, if active"
      (interactive)
      (let ((text (active-region-or-empty-string)))
        (mu4e-compose-new)
        (org-mu4e-compose-org-mode)
        (save-excursion
          (mu4e-compose-goto-top)
          (insert "#+OPTIONS: toc:nil num:nil\n")
          (insert text))))

    (defun active-region-or-empty-string ()
      (if (use-region-p)
          (buffer-substring-no-properties (mark) (point))
        ""))

    (if (file-directory-p "/usr/local/share/emacs/site-lisp/mu4e")
        (progn
          (use-package mu4e
            :load-path "/usr/local/share/emacs/site-lisp/mu4e"
            :config
            (setq mu4e-maildir "~/maildir"
                  mu4e-sent-folder "/Sent"
                  mu4e-drafts-folder "/Drafts"
                  mu4e-trash-folder "/Trash"
                  mu4e-refile-folder "/Archive"
                  mu4e-view-show-addresses t
                  mu4e-attachment-dir "~/downloads/attachments"
                  mu4e-use-fancy-chars t
                  mu4e-sent-messages-behavior 'sent
                  mu4e-compose-signature "\n/d"
                  mu4e-bookmarks
                  `( ,(make-mu4e-bookmark
                       :name "Unread Messages"
                       :query "flag:unread"
                       :key ?u)
                     ,(make-mu4e-bookmark
                       :name "All Inbox Messages"
                       :query "maildir:/"
                       :key ?i)
                     ,(make-mu4e-bookmark
                       :name "Emacs Mailinglist"
                       :query "maildir:/Emacs"
                       :key ?e)
                     ,(make-mu4e-bookmark
                       :name "Git Mailinglist"
                       :query "maildir:/Git"
                       :key ?g)
                     ,(make-mu4e-bookmark
                       :name "Archive"
                       :query "maildir:/Archive"
                       :key ?a)
                     ,(make-mu4e-bookmark
                       :name "Pending"
                       :query "maildir:/Pending"
                       :key ?p)
                     ,(make-mu4e-bookmark
                       :name "Trash"
                       :query "maildir:/Trash"
                       :key ?t)
                     ,(make-mu4e-bookmark
                       :name "Sent"
                       :query "maildir:/Sent"
                       :key ?s)))
            (global-set-key (kbd "C-c m m") 'mu4e-fetch-mail-and-mu4e)
            (global-set-key (kbd "C-c m n") 'mu4e-compose-mail-from-region))

          (use-package smtpmail
            :config
            (setq starttls-use-gnutls t
                  message-send-mail-function 'smtpmail-send-it
                  smtpmail-stream-type 'starttls)
            (load (expand-file-name "smtp_settings.el" user-emacs-directory)))

          (use-package org-mu4e
            :config
            (setq org-mu4e-convert-to-html t)
            (global-set-key (kbd "C-c m o") 'mu4e-compose-org-mail-from-region)
            (global-set-key (kbd "C-c m s") 'mu4e-compose-org-mail-from-section))))
  #+END_SRC

* Org mode
  Keep agenda files in =~/org/=.
  #+BEGIN_SRC emacs-lisp
    (setq org-agenda-files '("~/org"))
  #+END_SRC

  Define custom list of default TODO states:
  - =TODO= Task that is not yet started.
  - =ONGOING= Task that is currently being worked on.
  - =WAITING= Task that cannot be worked on since it is waiting for
    someone else to do something.
  - =ON HOLD= Task that has been started but is not being actively
    worked on.
  - =DONE= Completed task.
  - =CANCELLED= Task that will not be worked on.
  - =DELEGATED= Task that is delegated or reassigned.
  #+BEGIN_SRC emacs-lisp
    (setq org-todo-keywords
      '((sequence "TODO(t)" "ONGOING(o)" "WAITING(w)" "ON HOLD(h)" "|" "DONE(d)" "CANCELLED(c)" "DELEGATED(r)")))
  #+END_SRC

  Define global list of tags.
  #+BEGIN_SRC emacs-lisp
    (setq org-tag-persistent-alist
          '(("project" . ?p)))
  #+END_SRC

  Define custom capture templates.
  #+BEGIN_SRC emacs-lisp
    (defun capture-template-path (template-name &optional category)
      (let ((template-dir (expand-file-name "capture-templates" user-emacs-directory)))
        (if category
            (let ((category-dir (expand-file-name category template-dir)))
              (expand-file-name template-name category-dir))
          (expand-file-name template-name template-dir))))

    (setq org-capture-templates
          `(("l" "life")
            ("ll" "Todo" entry (file "~/org/life.org")
             (file ,(capture-template-path "todo")))
            ("lc" "Todo with checklist" entry (file "~/org/life.org")
             (file ,(capture-template-path "todo-checklist")))
            ("lo" "Org-mode Todo" entry (file+headline "~/org/life.org" "Org-mode things")
             (file ,(capture-template-path "todo-org" "life")))
            ("lb" "Book" entry (file+headline "~/org/life.org" "Books")
             (file ,(capture-template-path "books" "life")))
            ("lw" "Wishlist Item" entry (file+headline "~/org/life.org" "Wishlist")
             (file ,(capture-template-path "wishlist-item" "life")))
            ("lj" "Journal Entry" entry (file+datetree "~/org/journal.org")
             (file ,(capture-template-path "journal" "life")))
            ("w" "work")
            ("ww" "Todo" entry (file "~/org/work.org")
             (file ,(capture-template-path "todo")))
            ("wc" "Todo with checklist" entry (file "~/org/work.org")
             (file ,(capture-template-path "todo-checklist")))
            ("q" "quote" entry (file "~/org/quotes.org")
             (file ,(capture-template-path "quote")))))
  #+END_SRC

  Enable =flyspell-mode= and =auto-fill-mode= when writing journal
  entries.
  #+BEGIN_SRC emacs-lisp
    (add-hook 'org-capture-mode-hook
              (lambda ()
                (when (equal "CAPTURE-journal.org" (buffer-name))
                  (flyspell-mode)
                  (auto-fill-mode))))
  #+END_SRC
** Display
   Show nice bullets when not using terminal emacs.
   #+BEGIN_SRC emacs-lisp
     (when window-system
       (use-package org-bullets
         :ensure t
         :config
         (add-hook 'org-mode-hook (lambda ()
                                    (org-bullets-mode)))))
   #+END_SRC

   Align tags to 90 characters to allow longer headings.
   #+BEGIN_SRC emacs-lisp
     (setq org-tags-column 90)
   #+END_SRC

   Customize state colors.
   #+BEGIN_SRC emacs-lisp
     (setq org-todo-keyword-faces
           '(("TODO" . (:foreground "light coral" :weight bold))
             ("WAITING" . (:foreground "red" :weight bold))
             ("ONGOING" . (:foreground "deep sky blue" :weight bold))
             ("ON HOLD" . (:foreground "red" :weight bold))
             ("DONE" . (:foreground "spring green" :weight bold))
             ("CANCELLED" . (:foreground "dim gray" :weight bold))))
   #+END_SRC
** Code blocks
   Show syntax highlighting in code blocks.
   #+BEGIN_SRC emacs-lisp
     (setq org-src-fontify-natively t)
   #+END_SRC

   Make tab indent work in code blocks.
   #+BEGIN_SRC emacs-lisp
     (setq org-src-tab-acts-natively t)
   #+END_SRC

   Always allow running lisp blocks.
   #+BEGIN_SRC emacs-lisp
     (org-babel-do-load-languages
      'org-babel-load-languages
      '((emacs-lisp . t)))
   #+END_SRC
** Keybindings
   Bind  =C-c a= to =org-agenda= globally, so the agenda can be pulled up from
   anywhere.
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c a") 'org-agenda)
   #+END_SRC

   Bind =C-c c= to =org-capture= globally, since we are probably not doing org
   related things when thinking up new stuff...
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c c") 'org-capture)
   #+END_SRC

   Make =C-a= and =C-e= ignore leading stars and trailing
   tags. Hitting the key again will get the old behavior.
   #+BEGIN_SRC emacs-lisp
     (setq org-special-ctrl-a/e 'first)
   #+END_SRC
** Notes
   Log notes in the =NOTES= drawer, and add it as a drawer.
   #+BEGIN_SRC emacs-lisp
     (setq org-log-into-drawer "NOTES")
   #+END_SRC

   Log rescheduled tasks in the drawer too. Moving a deadline
   requires a note, moving a scheduled task only logs the time.
   #+BEGIN_SRC emacs-lisp
     (setq org-log-reschedule "time")
     (setq org-log-redeadline "note")
   #+END_SRC

   Ask for note when closing TODO's.
   #+BEGIN_SRC emacs-lisp
     (setq org-log-done 'note)
   #+END_SRC
** Export
   Use =xelatex= instead of =pdflatex= to build =.pdf= files from
   =.tex=:
   #+BEGIN_SRC emacs-lisp
     (setq org-latex-pdf-process
           '("xelatex -interaction nonstopmode -output-directory %o %f"
             "xelatex -interaction nonstopmode -output-directory %o %f"
             "xelatex -interaction nonstopmode -output-directory %o %f"))
   #+END_SRC
   Add more export engines:
   - Twitter bootstrap
     #+BEGIN_SRC emacs-lisp
       (use-package ox-twbs
         :ensure t)
     #+END_SRC
   - MediaWiki
     #+BEGIN_SRC emacs-lisp
       (use-package ox-mediawiki
         :ensure t)
     #+END_SRC
